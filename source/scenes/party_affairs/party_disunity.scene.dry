title: Party Disunity
new-page: true
is-card: true
view-if: party_disunity_timer = 0 and (dissent >= 0.3 or (right_dissent > 30 and not rightists_resign) or (center_dissent > 30 and not centrists_resign) or (left_dissent > 30 and not sapd_formed) or (labor_dissent > 30 and not unions_independent))
on-arrival: month_actions += 1; party_disunity_timer = 4
card-image: img/Reichstagsfraktion_der_SPD.jpg
frequency: 200
tags: party_affairs

# TODO: party dissent events for every faction, have a good image for this... 

= Party Disunity

The party is being torn apart by internal dissent. Upset at the leadership and general direction, factions have started sabotaging their rivals, hampering our efforts to win votes and carry out our policies.

[? if right_dissent > 30 : The rightists are upset at our direction.  ?]
[? if center_dissent > 30 : The centrists are displeased with the direction of our party. ?]
[? if labor_dissent > 30 : The trade unions disagree with our present course of action. ?]
[? if left_dissent > 30: Leftists disagree with our policies. ?]

Something must be done to reinforce party unity.

- @enforce_unity
- @concessions_right
- @concessions_center
- @concessions_left
- @concessions_labor
- @do_nothing


@enforce_unity
title: Enforce party discipline.
on-arrival: right_dissent -= 5; center_dissent -= 5; labor_dissent -= 5; left_dissent -= 5; workers_spd -= 4; new_middle_spd -= 2; unemployed_spd -= 2

Enforcing party discipline reduces dissent, but it could lead to an exit of dissenting members.

# enforcing party discipline applies to all parties.

# each party has a concessions action and an expulsions action.

@concessions_right
title: Give concessions to the right.
view-if: right_dissent > 30
on-arrival: right_dissent -= 7; left_dissent += 5

We have promised to adopt a more rightist posture. This may alienate the left, however.

@concessions_center
title: Give concessions to the centrists.
view-if: center_dissent > 30
on-arrival: center_dissent -= 7; right_dissent += 2; left_dissent += 2

We have promised to take more policy suggestions from the centrists. Both the left and right wings of the party might be displeased.

@concessions_left
title: Give concessions to the left.
view-if: left_dissent > 30
on-arrival: left_dissent -= 7; right_dissent += 5

We have promised that we will listen to the left wing of the party. Of course, this leads to increased dissent from the rightists.

@concessions_labor
title: Give concessions to labor.
view-if: labor_dissent > 30
on-arrival: labor_dissent -= 7; center_dissent += 5

The trade unions will be granted more influence in party decision making. This upsets the centrists, who would prefer to centralize political decision making within the party.


@do_nothing
title: Let the factions argue with themselves.
on-arrival: labor_dissent += 1; left_dissent += 1; center_dissent += 1; right_dissent += 1

Let the factions bicker; perhaps dissent is healthy for the party.

# TODO: should we be allowed to preemptively expel factions?

@expel_rightists
title: Expel the dissident rightists.
view-if: right_dissent > 30

@expel_left
title: Expel the dissident leftists.
view-if: left_dissent > 30

@expel_center
title: Expel the centrists.
view-if: center_dissent > 30

@expel_labor
title: Expel the labor supporters.
view-if: labor_dissent > 30
